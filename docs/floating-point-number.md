- [浮動小数点数](#浮動小数点数)
  - [誤差](#誤差)
  - [表計算ソフトにおける扱い](#表計算ソフトにおける扱い)
    - [床関数](#床関数)
    - [天井関数](#天井関数)
    - [謝辞](#謝辞)

# 浮動小数点数

実数を有限桁のビットで表現する方法のひとつに浮動小数点数がある。
今日において最も広く普及している規格は ISO/IEC/IEEE 60559(IEEE 754)だ。
IEEE754 では、用いるビット数別に以下の規格が定められている。

- binary32 (単精度浮動小数点数)
- binary64 (倍精度浮動小数点数)
- binary128 (四倍精度浮動小数点数)

ふつう、特記なしに浮動小数点数といえば IEEE754 binary64 を指す。
IEEE754 binary64 は十進小数をおよそ 15.95 桁の精度で表す。

<!-- ここでは IEEE754 binary64 に準拠した計算が正しいとする。 -->

## 誤差

浮動小数点数は実数を正確に表現できない。
これは、たとえば$\dfrac{1}{3}$を正確に有限桁の十進小数で書けないことと同様だ。
なお、浮動小数点数は 2 値のビットで表すため基数は 2 であり、$\dfrac{1}{10}$も正確に表現できない。

$\dfrac{1}{10}=0.00011001100110011\cdots_{2}$

他にも有名な例として、次がある。

$0.1 + 0.2 = 0.30000000000000004$

浮動小数点数にかかわる誤差には以下の種類がある。ここでは詳しく述べない。

- オーバーフロー／アンダーフロー
- 桁落ち
- 情報落ち
- 積み残し
- 丸め誤差

浮動小数点数に詳しい William Morton Kahan 氏曰く:

> 浮動小数点演算によって得られた結果と真値に大きな差が生じることは非常に稀であり、つねに心配するにはあまりにも稀であるが、だからといって無視できるほど稀なわけではない。

## 表計算ソフトにおける扱い

MS-Excel や Google spreadsheet などの表計算ソフトでは、数値を 15.95 桁精度から 15 桁精度へと丸める。例を挙げる。

- `1234567890123456`をセルに入力すると`1234567890123450`へと丸める。
- `1.234567890123456`をセルに入力すると`1.23456789012345`へと丸める。

### 床関数

任意の実数$x$に対して、$x$を越えない最大の整数$\lfloor x \rfloor$を求める。

標準で用意されている組み込み関数`int(･)`、`floor(･)`などでは正しく床関数をとれない。
これらの関数は 15 桁の精度に丸めてから期待する計算を行う。

```
=int(1.234567890123456)
↓ 15桁へ丸める
=int(1.23456789012345)
↓ 床関数をとる
=1
```

このことは次のような結果をもたらす。

```
=int(50 * 1.15 * 1.2)
↓ 15.95桁精度で乗算を行う
= int(68.99999999999999)
↓ 15桁精度へ丸める
=int(69)
↓床関数をとる
=69
```

数学の無限の精度での計算:

$\lfloor 50 \times 1.15 \times 1.2 \rfloor = 69$

これは真だ。しかし、艦これはこれを否定し 68 であると振る舞う。ゆえに「`50 * 1.15 * 1.2 == 68`は真」を得たい。

ここで、艦これの計算が浮動小数点演算を行っていると仮定し、さらに IEEE754 binary64 に準拠していると仮定して計算すると

$\lfloor 50 \times 1.15 \times 1.2 \rfloor = \lfloor 68.99999999999999 \rfloor = 68$

となる。
少なくともこの仮定のもとでは「`50 * 1.15 * 1.2 == 68`は真」を得られる。
これまでの検証において、この仮定に矛盾を発見していない。

さて、表計算ソフトにおいて 15.95 桁精度のままで床関数をとるには、15 桁への丸め条件を考慮すればよい。
一部の関数は 15.95 桁の精度のままで関数を作用するため、これらの関数を利用する。
以下でこれを達成できる。

```
=int(x) - not(gestep(x, int(x)))
```

この数式では実引数を与える箇所が 3 つあり、使い勝手が悪い。`lambda`や`let`を利用することで使いやすくできる。

```
=lambda(x, int(x) - not(gestep(x, int(x))))( /* ここに実引数を与える */ )
```

```
=let(x, /* ここに実引数を与える */, int(x) - not(gestep(x, int(x))))
```

```
=let(myFunc, lambda(x, int(x) - not(gestep(x, int(x)))), myFunc(/* ここに実引数を与える */))
```

### 天井関数

任意の実数$x$に対して、$x$を越える最小の整数$\lceil x \rceil$を求める。

標準で用意されている組み込み関数`ceiling(･)`などでは正しく天井関数をとれない。
これらの関数は 15 桁の精度に丸めてから期待する計算を行う。
[床関数](#床関数)での手法と同様に、一部の関数は 15.95 桁の精度のままで関数を作用するため、これらの関数を利用する。
以下でこれを達成できる。

```
=ceiling(x) + not(gestep(ceiling(x), x))
```

この数式では実引数を与える箇所が 3 つあり、使い勝手が悪い。`lambda`や`let`を利用することで使いやすくできる。

```
=lambda(x, ceiling(x) + not(gestep(ceiling(x), x)))(/* ここに実引数を与える */)
```

```
=let(x, /* ここに実引数を与える */, ceiling(x) + not(gestep(ceiling(x), x)))
```

```
=let(myFunc, lambda(x, ceiling(x) + not(gestep(ceiling(x), x))), myFunc(/* ここに実引数を与える */))
```

### 謝辞

表計算ソフトにおける"お節介な"数値丸めに対する是正は、長らくの間大きな課題であった。ここで示した是正方法は筆者が発見した。
しかし、こうした是正が必要であることを知らなければこの是正方法を発見できずに、ダメージ検証はその結果の正確性を欠いていただろう。
表計算ソフトでは浮動小数点演算に際して課題があることを示したうみゃ氏に深く感謝する。
